
class ToM_LaserBeam : Actor
{
	Color shade;

	double maxDist;
	int ontics;
	bool enabled;
	Actor source;
	vector3 offsets;
	vector2 angleOffsets;
	transient FLineTraceData hitData;
	
	bool trackingPos;
	vector3 targetPos;
	
	bool aimWithWeapon;
	bool trackPSprite;
	uint trackPSLayer;
	
	bool followAngles;
	Property TrackAngles : followAngles;
	Property TrackWeapon : trackPSprite, trackPSLayer;
	Property AimFromWeapon : aimWithWeapon; 
	Property LaserColor : shade;
	
	Default
	{
		Scale 1.0;
		+NOINTERACTION;
		+INTERPOLATEANGLES;
		RenderStyle "AddShaded";
		
		ToM_LaserBeam.LaserColor "Blue";
		ToM_LaserBeam.TrackAngles true;				// Update with player's view.
		ToM_LaserBeam.TrackWeapon true, PSP_WEAPON; // Offset by PSprite offsets.
		ToM_LaserBeam.AimFromWeapon true;			// Fire from weapon "muzzle", only used if TrackWeapon is enabled.
	}
	
	static ToM_LaserBeam Create(Actor source, double fw, double lr, double ud, double angleOffs = 0, double pitchOffs = 0, double maxDist = 2048, class<ToM_LaserBeam> type = "ToM_LaserBeam")
	{
		let laser = ToM_LaserBeam(Spawn(type, source.pos));
		if(laser) 
		{
			laser.source = source;
			laser.maxDist = maxDist;
			laser.offsets = (fw, lr, ud);
			laser.angleOffsets = (angleOffs, pitchOffs);
		}
		
		return laser;
	}
	
	void setEnabled(bool set)
	{
		enabled = set;
	}
	
	void startTracking(vector3 toPos)
	{
		trackingPos = true;
		targetPos = toPos;
	}
	
	void stopTracking()
	{
		trackingPos = false;
	}
	
	bool isTracking()
	{
		return trackingPos;
	}
	
	virtual vector3 getSourcePos()
	{
		vector3 srcPos = (source.pos.xy, source.pos.z + (source.height * 0.5));
		if(source.player) srcPos.z = source.player.viewz;
		
		return srcPos;
	}
	
	virtual void BeamTick()
	{
		// Implement custom laser logic here.
	}
	
	override void Tick()
	{
		if(isFrozen()) return;
		if(!enabled) 
		{
			ontics = 0;
			bInvisible = true;
			return;
		}
		ontics++;
		bInvisible = ontics < 3;
		if(shade) SetShade(shade);
				
		// PSprite tracking?
		vector2 bob = (0,0);
		if(trackPSprite)
		{
			let psp = source.player.GetPSprite(trackPSLayer);
			bob = PlayerPawn(source).BobWeapon(1.0);
			
			bob.x *= 0.031;
			bob.y *= 0.035;
			bob.x += psp.x;
			bob.y += (psp.y - 32);
		}
		
		// Update laser and tracking.
		vector3 offsPos = ToM_BeamzMath.offsetToGlobal(
			source.angle, source.pitch, source.roll,
			offsets.x, offsets.y + bob.x, offsets.z - bob.y
		);
		vector3 finalPos = level.vec3offset(getSourcePos(), offsPos);
		SetOrigin(finalPos, true);
		
		if(trackingPos)
		{
			vector3 diff = level.vec3diff(finalPos, targetPos);
			vector3 dir = diff.Unit();
			
			scale.y = diff.Length() * level.pixelstretch;
			
			double toAngle = atan2(dir.y, dir.x) + 180; 
			double toPitch = asin(dir.z);
			A_SetAngle(toAngle, SPF_INTERPOLATE);
			A_SetPitch(toPitch - 90, SPF_INTERPOLATE);
			return;
		}
		
		double toAngle = source.angle + angleOffsets.x;
		double toPitch = source.pitch + angleOffsets.y;
		if(aimWithWeapon) 
		{
			toAngle -= (bob.x * 10);
			toPitch += (bob.y * 10);  
		}		
		
		// Update angles
		if(followAngles)
		{
			// Credits: This interpolation magic was gleaned from Kodi's KLazer library
			// code available in PainSlayer 
			// (https://github.com/jekyllgrim/Painslayer/blob/master/ZPainkiller/klazer/KLAZ_Beam.zsc#L114)
				
			// First, track angles like normal.
			A_SetAngle(toAngle, SPF_INTERPOLATE);
			A_SetPitch(toPitch+90, SPF_INTERPOLATE);
		
			// Odd interpolation magic.
			double prevAngle = source.angle;
			source.A_SetAngle(source.angle + 0.001, SPF_INTERPOLATE);
			source.A_SetAngle(prevAngle, SPF_INTERPOLATE);
		}
		
		// Do linetrace to determine out aim distance.
		double zoffs = source.player ? (source.player.viewz-source.pos.z) : source.height * 0.5;
		source.LineTrace(angle, maxDist, pitch - 90, 0, zoffs+offsets.z-bob.y, offsets.x, offsets.y-bob.x, data:hitData);
		
		double dist = min(hitData.Distance, maxDist);
		double dirPitch = pitch - 90;
		vector3 dir = ToM_BeamzMath.GetAxes(angle, pitch - 90, roll);
		scale.y = dist * level.pixelstretch;
		
		BeamTick();
	}
	
	States
	{
		Spawn:
			MODL A -1 Bright;
		stop;
	}
}
